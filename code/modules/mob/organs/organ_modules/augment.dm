/obj/item/organ_module
	name = "embedded organ module"
	desc = "A mechanical augment designed for implantation into a human's flesh or prosthetics."
	icon = 'icons/obj/implants.dmi'
	matter = list(MATERIAL_STEEL = 12)
	w_class = ITEM_SIZE_TINY
	/// List of organ tags
	var/list/allowed_organs = list()
	/// For cybernetic-specific applicator sprite calls.
	var/mod_overlay
	/// Movspeed modifier added to organ's overall tally.
	var/organ_tally = 0

	var/toggled = FALSE

	var/module_flags = OM_FLAG_SCANNABLE | OM_FLAG_MECHANICAL // Can be replaced with OM_FLAG_DEFAULT

	/// If not null (see defines), will prevent installing more than one module of the same type into one prosthesis.
	var/module_type = null

	var/cooldown = (0.5 SECONDS)

	/// CPU power generated by this augment.
	var/cpu_power
	/// CPU power consumed by this augment
	var/cpu_load = 1

	/// The number of loadout points for selecting this module in char setup.
	var/loadout_cost = 6
	/// Whether this module can be selected in character setup menu
	var/available_in_charsetup = TRUE
	var/list/allowed_jobs

/obj/item/organ_module/proc/install(obj/item/organ/E)
	E.implants += src
	E.organ_modules += src
	E.occupied_space += w_class
	forceMove(E)
	_on_install(E)
	post_install(E)

/obj/item/organ_module/proc/_on_install(obj/item/organ/E)
	if(organ_tally)
		var/obj/item/organ/external/ex = E
		ex?.update_tally()

/// Called after module is installed. Override for custom behavior.
/obj/item/organ_module/proc/post_install(obj/item/organ/E)
	SHOULD_CALL_PARENT(FALSE)
	pass()

/obj/item/organ_module/proc/remove(obj/item/organ/E)
	E.implants -= src
	E.organ_modules -= src
	forceMove(E.drop_location())
	_on_remove(E)
	post_removed(E)

/obj/item/organ_module/proc/_on_remove(obj/item/organ/E)
	if(organ_tally)
		var/obj/item/organ/external/ex = E
		ex?.update_tally()

/// Called after module is removed. Override for custom behavior.
/obj/item/organ_module/proc/post_removed(obj/item/organ/E)
	SHOULD_CALL_PARENT(FALSE)
	pass()

/obj/item/organ_module/proc/organ_removed()
	pass()

/obj/item/organ_module/proc/organ_installed()
	pass()
