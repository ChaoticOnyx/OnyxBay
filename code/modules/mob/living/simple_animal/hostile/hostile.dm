/mob/living/simple_animal/hostile
	faction = "hostile"
	var/stance = HOSTILE_STANCE_IDLE	//Used to determine behavior
	var/mob/living/target_mob
	var/attack_same = 0
	var/ranged = 0
	var/rapid = 0
	var/projectiletype
	var/projectilesound
	var/casingtype
	var/move_to_delay = 4 //delay for the automated movement.
	var/attack_delay = DEFAULT_ATTACK_COOLDOWN
	var/list/friends = list()
	var/break_stuff_probability = 10
	stop_automated_movement_when_pulled = 0
	var/destroy_surroundings = 1
	a_intent = I_HURT
	armor_projectile = 25

	mouse_opacity = 2 //This makes it easier to hit hostile mobs, you only need to click on their tile, and is set back to 1 when they die
	var/vision_range = 7 //How big of an area to search for targets in, a vision of 7 attempts to find targets as soon as they walk into screen view

	var/aggro_vision_range = 7 //If a mob is aggro, we search in this radius. Defaults to 7 to keep in line with original simple mob aggro radius
	var/idle_vision_range = 7 //If a mob is just idling around, it's vision range is limited to this. Defaults to 7 to keep in line with original simple mob aggro radius
	var/retreat_distance = null //If our mob runs from players when they're too close, set in tile distance. By default, mobs do not retreat.
	var/minimum_distance = 1 //Minimum approach distance, so ranged mobs chase targets down, but still keep their distance set in tiles to the target, set higher to make mobs keep distance
	var/ranged_message = "fires" //Fluff text for ranged mobs
	var/ranged_cooldown = 0 //What the starting cooldown is on ranged attacks
	var/ranged_cooldown_cap = 3 //What ranged attacks, after being used are set to, to go back on cooldown, defaults to 3 life() ticks

	var/shuttletarget = null
	var/enroute = 0

/mob/living/simple_animal/hostile/proc/FindTarget()
	if(!faction || client) //No faction, no reason to attack anybody.
		return null
	var/atom/T = null
	stop_automated_movement = 0
	for(var/atom/A in ListTargets(10))

		if(A == src)
			continue

		var/atom/F = Found(A)
		if(F)
			T = F
			break

		if(isliving(A))
			var/mob/living/L = A
			if(L.faction == src.faction && !attack_same)
				continue
			else if(L in friends)
				continue
			else
				if(!L.stat)
					if (ishuman(L))
						var/mob/living/carbon/human/H = L
						if (H.is_cloaked())
							continue
					Aggro()
					stance = HOSTILE_STANCE_ATTACK
					T = L
					break

		else if(istype(A, /obj/mecha)) // Our line of sight stuff was already done in ListTargets().
			var/obj/mecha/M = A
			if (M.occupant)
				Aggro()
				stance = HOSTILE_STANCE_ATTACK
				T = M
				break
	return T


/mob/living/simple_animal/hostile/proc/Found(atom/A)
	Aggro()
	return

/mob/living/simple_animal/hostile/proc/GiveTarget(new_target)
	target_mob = new_target
	if(target_mob != null)
		Aggro()
		stance = HOSTILE_STANCE_ATTACK

/mob/living/simple_animal/hostile/adjustBruteLoss(damage)
	. = ..(damage)
	if(!stat)
		if(stance == HOSTILE_STANCE_IDLE)//If we took damage while idle, immediately attempt to find the source of it so we find a living target
			Aggro()
			if(!client)
				GiveTarget(FindTarget())
		if(stance == HOSTILE_STANCE_ATTACK)//No more pulling a mob forever and having a second player attack it, it can switch targets now if it finds a more suitable one
			if(target_mob != null && prob(25) && !client)
				GiveTarget(FindTarget())

/mob/living/simple_animal/hostile/proc/MoveToTarget()
	stop_automated_movement = 1
	if(!target_mob || SA_attackable(target_mob))
		stance = HOSTILE_STANCE_IDLE
	if(target_mob in ListTargets(10))
		var/target_distance = get_dist(src, target_mob)
		if(ranged)
			if(target_distance >= 2 && ranged_cooldown <= 0)
				OpenFire(target_mob)
		if(retreat_distance != null)//If we have a retreat distance, check if we need to run from our target
			if(target_distance <= retreat_distance)//If target's closer than our retreat distance, run
				walk_away(src, target_mob, retreat_distance, move_to_delay)
			else
				Goto(target_mob, move_to_delay, minimum_distance)//Otherwise, get to our minimum distance so we chase them
		else
			stance = HOSTILE_STANCE_ATTACKING
			Goto(target_mob, move_to_delay, minimum_distance)
	if(target_mob.loc != null && get_dist(src, target_mob.loc) <= vision_range)//We can't see our target, but he's in our vision range still
		Goto(target_mob, move_to_delay, minimum_distance)

/mob/living/simple_animal/hostile/proc/Goto(target_mob, delay, minimum_distance)
	walk_to(src, target_mob, minimum_distance, delay)

/mob/living/simple_animal/hostile/proc/AttackTarget()
	stop_automated_movement = 1
	if(!target_mob || SA_attackable(target_mob))
		LoseTarget()
		return 0
	if(!(target_mob in ListTargets(10)))
		LostTarget()
		return 0
	if (ishuman(target_mob))
		var/mob/living/carbon/human/H = target_mob
		if (H.is_cloaked())
			LoseTarget()
			return 0
	if(next_move >= world.time)
		return 0
	if(ranged)
		if(get_dist(src, target_mob) >= 2 && ranged_cooldown <= 0)
			OpenFire(target_mob)
	if(get_dist(src, target_mob) <= 1)	//Attacking
		AttackingTarget()
		return 1

/mob/living/simple_animal/hostile/proc/AttackingTarget()
	setClickCooldown(attack_delay)
	if(!Adjacent(target_mob))
		return
	if(isliving(target_mob))
		var/mob/living/L = target_mob
		L.attack_generic(src, rand(melee_damage_lower, melee_damage_upper), attacktext, environment_smash, damtype, defense)
		return L
	if(istype(target_mob,/obj/mecha))
		var/obj/mecha/M = target_mob
		M.attack_generic(src, rand(melee_damage_lower, melee_damage_upper), attacktext)
		return M

/mob/living/simple_animal/hostile/proc/Aggro()
	if(stat == DEAD)
		return 0
	vision_range = aggro_vision_range
	return 1

/mob/living/simple_animal/hostile/proc/LoseAggro()
	if(stat == DEAD)
		return 0
	stop_automated_movement = 0
	vision_range = idle_vision_range
	return 1

/mob/living/simple_animal/hostile/proc/LoseTarget()
	stance = HOSTILE_STANCE_IDLE
	target_mob = null
	walk(src, 0)
	LoseAggro()

/mob/living/simple_animal/hostile/proc/LostTarget()
	stance = HOSTILE_STANCE_IDLE
	walk(src, 0)
	LoseAggro()

/mob/living/simple_animal/hostile/proc/ListTargets()
	var/list/L = hearers(src, vision_range)

	for (var/obj/mecha/M in mechas_list)
		if (M.z == src.z && get_dist(src, M) <= vision_range && can_see(src, M, vision_range))
			L += M

	return L

/mob/living/simple_animal/hostile/death(gibbed, deathmessage, show_dead_message)
	. = ..()
	if(.)
		LoseAggro()
		mouse_opacity = 1

/mob/living/simple_animal/hostile/Life()
	. = ..()
	if(!.)
		walk(src, 0)
		return 0
	if(client)
		return 0
	if(isturf(loc) && !buckled)
		if(!stat)
			switch(stance)
				if(HOSTILE_STANCE_IDLE)
					target_mob = FindTarget()

				if(HOSTILE_STANCE_ATTACK)
					if(destroy_surroundings)
						DestroySurroundings()
					MoveToTarget()

				if(HOSTILE_STANCE_ATTACKING)
					if(destroy_surroundings)
						DestroySurroundings()
					AttackTarget()
				if(HOSTILE_STANCE_INSIDE) //we aren't inside something so just switch
					stance = HOSTILE_STANCE_IDLE
			if(ranged && ranged_cooldown)
				ranged_cooldown--
	else
		if(stance != HOSTILE_STANCE_INSIDE)
			stance = HOSTILE_STANCE_INSIDE
			walk(src, 0)
			target_mob = null
	if(!target_mob)
		LoseAggro()

/mob/living/simple_animal/hostile/attackby(obj/item/O, mob/user)
	. = ..()
	var/oldhealth = health
	if(health < oldhealth && !incapacitated(INCAPACITATION_KNOCKOUT) && !client)
		target_mob = user
		MoveToTarget()

/mob/living/simple_animal/hostile/attack_hand(mob/living/carbon/human/M)
	. = ..()
	if(M.a_intent == I_HURT && !incapacitated(INCAPACITATION_KNOCKOUT) && !client)
		target_mob = M
		MoveToTarget()

/mob/living/simple_animal/hostile/bullet_act(obj/item/projectile/Proj)
	. = ..()
	var/oldhealth = health
	if(!target_mob && health < oldhealth && !incapacitated(INCAPACITATION_KNOCKOUT) && !client)
		target_mob = Proj.firer
		MoveToTarget()

/mob/living/simple_animal/hostile/proc/OpenFire(target_mob)

	var/target = target_mob
	visible_message("\red <b>[src]</b> [ranged_message] at [target]!", 1)

	if(rapid)
		spawn(1)
			Shoot(target, src.loc, src)
			if(casingtype)
				new casingtype(get_turf(src))
		spawn(4)
			Shoot(target, src.loc, src)
			if(casingtype)
				new casingtype(get_turf(src))
		spawn(6)
			Shoot(target, src.loc, src)
			if(casingtype)
				new casingtype(get_turf(src))
	else
		Shoot(target, src.loc, src)
		if(casingtype)
			new casingtype
	ranged_cooldown = ranged_cooldown_cap
	return

/mob/living/simple_animal/hostile/proc/Shoot(target, start, user, bullet = 0)
	if(target == start)
		return

	var/obj/item/projectile/A = new projectiletype(user:loc)
	playsound(user, projectilesound, 100, 1)
	if(!A)	return
	var/def_zone = get_exposed_defense_zone(target)
	A.launch(target, def_zone)

/mob/living/simple_animal/hostile/proc/DestroySurroundings()
	if(prob(break_stuff_probability))
		for(var/dir in GLOB.cardinal) // North, South, East, West
			var/obj/effect/shield/S = locate(/obj/effect/shield, get_step(src, dir))
			if(S && S.gen && S.gen.check_flag(MODEFLAG_NONHUMANS))
				S.attack_generic(src, rand(melee_damage_lower, melee_damage_upper), attacktext)
				return
			for(var/obj/structure/window/obstacle in get_step(src, dir))
				if(obstacle.dir == GLOB.reverse_dir[dir]) // So that windows get smashed in the right order
					obstacle.attack_generic(src, rand(melee_damage_lower, melee_damage_upper), attacktext)
					return
			var/obj/structure/obstacle = locate(/obj/structure, get_step(src, dir))
			if(istype(obstacle, /obj/structure/window) || istype(obstacle, /obj/structure/closet) || istype(obstacle, /obj/structure/table) || istype(obstacle, /obj/structure/grille))
				obstacle.attack_generic(src, rand(melee_damage_lower, melee_damage_upper), attacktext)

/mob/living/simple_animal/hostile/on_ghost_possess()
	LoseTarget()
